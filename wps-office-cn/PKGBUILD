# Maintainer: Astro Benzene <universebenzene at sina dot com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ouyang Jun <ouyangjun1999@gmail.com>
# Contributor: Jove Yu <yushijun110 [at] gmail.com>
# Contributor: Ariel AxionL <axionl at aosc dot io>

pkgname=('wps-office-mui-zh-cn')
pkgver=11.1.0.10161
pkgrel=1
pkgdesc="Kingsoft Office (WPS Office) CN version - an office productivity suite"
arch=('x86_64' 'aarch64')
license=('custom')
url="https://linux.wps.cn"
options=('!emptydirs')
source_x86_64=("https://wdl1.cache.wps.cn/wps/download/ep/Linux2019/${pkgver##*.}/wps-office_${pkgver}_amd64.deb")
source_aarch64=("https://wdl1.cache.wps.cn/wps/download/ep/Linux2019/${pkgver##*.}/wps-office_${pkgver}_arm64.deb")
sha1sums_x86_64=('a0a715cf6932b20715b7164f549ed7a24e29af4a')
sha1sums_aarch64=('74f73aeb83de2eec716e71408b8df3c0ce1cf46e')

prepare() {
    bsdtar -xpf data.tar.xz

    cd "${srcdir}/usr/bin"
    sed -i 's|/opt/kingsoft/wps-office|/usr/lib|' *

    cd "${srcdir}/usr/share/icons/hicolor"
    for _file in ./*; do
        if [ -e ${_file}/mimetypes/wps-office2019-etmain.png ]; then
            mkdir -p ${_file}/apps
            cp -p ${_file}/mimetypes/wps-office2019* ${_file}/apps
        fi
    done
}

package() {
    pkgdesc="Chinese (Simplified) mui package for WPS Office"
    cd "${srcdir}/opt/kingsoft/wps-office/office6/mui"

    install -d "${pkgdir}/usr/lib/office6/mui/en_US/resource"
    cp -r en_US/resource/help "${pkgdir}/usr/lib/office6/mui/en_US/resource"
    cp -r zh_CN "${pkgdir}/usr/lib/office6/mui"

    cd "${srcdir}/opt/kingsoft/wps-office/"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" office6/mui/default/*.html
}
